import com.google.common.io.Files

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Settings
ext {
    project_group = 'com.github.blacklocus'
    project_version = '0.1.4-SNAPSHOT'
    project_jdk = '1.7'
    project_pom = {
        name 'rds-echo'
        description 'Tooling for automated RDS restorations from snapshots'
        url 'https://github.com/blacklocus/rds-echo'
        licenses {
            license {
                name 'MIT License'
                url 'http://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/blacklocus/rds-echo.git'
            connection 'scm:git:https://github.com/blacklocus/rds-echo.git'
            developerConnection 'scm:git:git@github.com:blacklocus/rds-echo.git'
        }
        organization {
            name 'BlackLocus'
            url 'https://github.com/blacklocus'
        }
        developers {
            developer {
                id 'dirkraft'
                name 'Jason Dunkelberger'
            }
        }
    }

    /** Function always returns a new manifest that can be customized */
    defaultManifest = {
        return manifest {
            def git_cmd = "git rev-parse HEAD"
            def git_proc = git_cmd.execute()
            attributes 'SCM-Revision': git_proc.text.trim()
            attributes 'Timestamp': String.valueOf(System.currentTimeMillis())
            attributes 'Build-Host': InetAddress.localHost.hostName
        }
    }

    defaultBlank = { closure ->
        try {
            closure()
        } catch (MissingPropertyException e) {
            ''
        }
    }

}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// License

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
    }
}
apply plugin: 'idea'
apply plugin: 'license'

license {
    header rootProject.file('src/license/HEADER')
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Identifiers

group = project_group
version = project_version
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Plugins

apply plugin: 'java'
sourceCompatibility = project_jdk
targetCompatibility = project_jdk
apply plugin: 'application'

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.rholder:gradle-autojar:1.0.1'
    }
}
apply plugin: 'gradle-autojar'

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Dependencies

configurations {
    provided
}

sourceSets {
    main {
        runtimeClasspath += configurations.provided
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.amazonaws:aws-java-sdk-rds:1.9.16'
    compile 'com.amazonaws:aws-java-sdk-route53:1.9.16'
    compile 'commons-configuration:commons-configuration:1.10'
    compile 'com.google.guava:guava:18.0'
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'org.slf4j:slf4j-simple:1.7.10'

    testCompile 'org.testng:testng:6.8.17'
    // Because can output line numbers
    testCompile 'ch.qos.logback:logback-core:1.1.2'
    testCompile 'ch.qos.logback:logback-classic:1.1.2'

}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Artifacts

jar {
    doFirst {
        // Timestamp changes on every build. By evaluating it later, won't needlessly fail up-to-date checks.
        manifest = defaultManifest()
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
    doFirst {
        // Timestamp changes on every build. By evaluating it later, won't needlessly fail up-to-date checks.
        manifest = defaultManifest()
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    doFirst {
        // Timestamp changes on every build. By evaluating it later, won't needlessly fail up-to-date checks.
        manifest = defaultManifest()
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Publishing

assemble {
    dependsOn licenseFormatMain, licenseFormatTest
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    dependsOn licenseFormatMain, licenseFormatTest
    enabled = false // by default, mark individual projects back to true for inclusion in publishing

    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        //# ./gradlew -PdeployUrl=http://server/artifactory/repo -PdeployUsername=admin -PdeployPassword=pass uploadArchives
        // for snapshots https://oss.sonatype.org/content/repositories/snapshots
        // for staging/release https://oss.sonatype.org/service/local/staging/deploy/maven2
        repository(
                url: defaultBlank({ deployUrl })
        ) {
            // If these are not defined assemble needlessly fails for unrelated tasks, hence, defaultBlank.
            authentication(userName: defaultBlank({ deployUsername }), password: defaultBlank({ deployPassword }))
        }

        pom.project project_pom
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Misc

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

idea {
    module {
        downloadSources = true
    }
    project {
        jdkName = project_jdk
        languageLevel = project_jdk
        vcs = 'Git'
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// RDS Echo specific

def findJar(prefix) {
    return configurations.compile.filter { it.name.startsWith(prefix) }
}

task prepareJodaTimezones(type: Copy) {
    group = "Distribution"
    description = "Extract joda-time timezone data files out to the build directory"

    def jarFile = findJar('joda-time').singleFile
    def outputDir = new File(buildDir, "joda-time")

    from zipTree(jarFile)
    into outputDir
    include 'org/joda/time/tz/data/**'
}

task cliAutojar(type: Autojar, dependsOn: prepareJodaTimezones) {
    group = "Distribution"
    description = "Generate a stripped down Autojar'd executable jar"

    mainClass = "com.github.blacklocus.rdsecho.Echo"
    manifest = defaultManifest()

    autojarFiles = [
            "com/amazonaws/sdk/versionInfo.properties",
            "com/amazonaws/regions/regions.xml",
            "awssdk_config_default.json",
            "rdsecho.properties.sample",
            "simplelogger.properties"]

    autojarClasses = [
            "org.slf4j.MDC",
            "org.slf4j.impl.StaticMDCBinder",
            "org.apache.commons.logging.impl.SimpleLog",
            "org.apache.commons.logging.impl.LogFactoryImpl",
            "com.amazonaws.internal.config.HttpClientConfigJsonHelper",
            "com.amazonaws.internal.config.HostRegexToRegionMappingJsonHelper"]

    doFirst {
        prepareJodaTimezones.outputs.files.each {
            fileTree(it).visit {
                if(!it.isDirectory()) {
                    autojarFiles.add(it.relativePath)
                }
            }
        }
    }
}

task cliDistAutojar(dependsOn: cliAutojar) {
    group = "Distribution"
    description = "Create a distributable standalone executable binary"

    ext.bashFile = file('src/main/bash/rdsecho.bash')
    ext.jarFile = cliAutojar.outputs.files.singleFile
    ext.cliFile = file('build/libs/rds-echo')
    inputs.files bashFile, jarFile
    outputs.file cliFile

    doLast {
        cliFile.createNewFile()
        cliFile.setExecutable(true, false)
        def fileOutputStream = new FileOutputStream(cliFile, true)
        fileOutputStream.withCloseable {
            Files.copy(bashFile, fileOutputStream)
            Files.copy(jarFile, fileOutputStream)
            fileOutputStream.flush()
        }
    }
}
